defmodule Exmud.Engine.Command.ExecutionContext do
  @moduledoc """
  An ExecutionContext struct contains everything required for the processing of a Command.

  The context is intended to be passed between multiple middlewares, some of which may need to run before others to
  populate required data.
  """

  @enforce_keys [:caller, :raw_input]
  defstruct [
    # Parsed arguments for the command.
    :args,
    # Object that is doing the calling. All Commands are executed by Objects, even if triggered by a Player. In the case
    # of the Player, the Object that performs the action is the Object/Character that they are puppeting.
    :caller,
    # Arbitrary data that can be set and used from any of the middlewares.
    {:data, %{}},
    # List of Commands generated by merging all accessible Command Sets.
    :command_list,
    # The Command which was actually matched.
    :matched_command,
    # Key that actually matched. For 'move west' the matched_key might be 'move'.
    :matched_key,
    # Events to be executed as part of the pipeline. Events can be added by any of the middlewares including the Command
    # being executed. All events will be processed after--and only if--the transaction commits successfully.
    {:events, []},
    # The Object the matched Command belongs to. Does not have to be the caller.
    :owner,
    # The raw text input before any processing.
    :raw_input,
    # The raw argument string, which is the raw_input minus the matched command.
    :raw_args
  ]

  @type t :: %Exmud.Engine.Command.ExecutionContext{
          args: term,
          command_list: [Exmud.Engine.Command.t()],
          matched_command: Exmud.Engine.Command.t(),
          matched_key: String.t(),
          events: [Exmud.Engine.Event.t()],
          owner: integer,
          raw_input: String.t(),
          raw_args: String.t(),
          caller: integer,
          data: Map.t()
        }

  @doc """
  Get a value from the data embedded in the Context.
  """
  @spec get(%__MODULE__{}, String.t() | atom()) :: any()
  def get(context, key) do
    Map.get(context.data, key)
  end

  @doc """
  Check to see if a key exists in the data of the Context.
  """
  @spec has_key?(%__MODULE__{}, String.t() | atom()) :: boolean()
  def has_key?(context, key) do
    Map.has_key?(context.data, key)
  end

  @doc """
  Put a value into the data embedded in the Context, returning the updated Context.
  """
  @spec put(%__MODULE__{}, String.t() | atom(), term()) :: %__MODULE__{}
  def put(context, key, value) do
    %{context | data: Map.put(context.data, key, value)}
  end
end

defimpl String.Chars, for: Exmud.Engine.Command.ExecutionContext do
  @spec to_string(%Exmud.Engine.Command.ExecutionContext{}) :: String.t()
  def to_string(context) do
    "%{caller: '#{context.caller}', matched_key:'#{context.matched_key}'," <>
      " owner: '#{context.owner}', raw_input: '#{context.raw_input}'}"
  end
end
